plugins {
    id 'com.android.application'
}

/*
 * This program is the client app for Team Unununium's VR Robot Explorer found at <https://github.com/team-unununium>
 * Copyright (C) 2020 Team Unununium
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/> .
 */

// Load keystore
def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file('keystore.properties')
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ignored) {
    keystoreProperties.put('keystoreDir', '/')
}

// Load string constants
def secureProperties = new Properties()
try {
    def securePropertiesFile = rootProject.file('secure.properties')
    secureProperties.load(new FileInputStream(securePropertiesFile))
} catch (Exception ignored) {

}

android {
    signingConfigs {
        release {
            storeFile = file(keystoreProperties['keystoreDir'])
            keyAlias = keystoreProperties['keystoreAlias']
            keyPassword = keystoreProperties['keystorePass']
            storePassword = keystoreProperties['keystorePass']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.unununium"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "0.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Custom variables
        buildConfigField "String", "SERVER_OPERATOR_SECRET", "${secureProperties["SERVER_OPERATOR_SECRET"]}"
        buildConfigField "String", "SERVER_CLIENT_SECRET", "${secureProperties["SERVER_CLIENT_SECRET"]}"
        buildConfigField "String", "SERVER_URL", "${secureProperties["SERVER_URL"]}"
        resValue "string", "app_name", "Unununium Client"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            versionNameSuffix = '-d'
            sourceSets {
                androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
            }
            resValue "string", "app_name", "UC Debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.android.volley:volley:1.2.0'
    implementation 'com.github.AppIntro:AppIntro:6.0.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation ('io.socket:socket.io-client:2.0.1') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
